Q1. Find all pizzas eaten by at least one female over the age of 20.

\project_{pizza} (
     (\select_{gender='female' and age>20} Person)
     \join Eats
);


Q2. Find the names of all females who eat at least one pizza served by Straw Hat. (Note: The pizza need not be eaten at Straw Hat.) 

\project_{name} (
    ((\select_{gender='female'} Person) \join Eats)
    \join 
    (\select_{pizzeria='Straw Hat'} Serves)
);

Q3. Find all pizzerias that serve at least one pizza for less than $10 that either Amy or Fay (or both) eat. 

\project_{pizzeria} (
    ((\select_{name='Amy' or name='Fay'} Person) \join Eats)
    \join
    (\select_{price<10} Serves)
);


Q4. Find all pizzerias that serve at least one pizza for less than $10 that both Amy and Fay eat. 

\project_{pizzeria} (
    \project_{pizza1} (
		(
			(\rename_{name1, age1, gender1, pizza1} (
				(\select_{name='Amy'} Person) \join Eats)
			)
			\join_{pizza1=pizza2}
			(\rename_{name2, age2, gender2, pizza2} (
				(\select_{name='Fay'} Person) \join Eats)
			)
		)
    )
    \join_{pizza1=pizza} (\select_{price<10} Serves)
);


Q5. Find the names of all people who eat at least one pizza served by Dominos but who do not frequent Dominos. 

\project_{name} (
    ((\project_{name} Eats)
    \diff
    (\project_{name} 
		(\select_{pizzeria='Dominos'} Frequents))
    )
    \join
    ((\select_{pizzeria='Dominos'} Serves) \join Eats)
);


Q6. Find all pizzas that are eaten only by people younger than 24, or that cost less than $10 everywhere they're served.

\project_{*} (
    ((\project_{pizza}
        ((\select_{age<24} Person) \join Eats))
    \diff
    (\project_{pizza}
        ((\select_{age>=24} Person) \join Eats)))
        
    \union
    
    ((\project_{pizza}
        (\select_{price<10} Serves))
    \diff
    (\project_{pizza}
        (\select_{price>=10} Serves)))
);


Q7. Find the age of the oldest person (or people) who eat mushroom pizza. 

\project_{*} (	
	(\project_{age} (
		Person
		\join
		(\select_{pizza='mushroom'} Eats)
	))
	
	\diff
	
	\project_{age1} (
		(\select_{age1<age2} (
			\rename_{age1} (\project_{age} (
				Person
				\join
				(\select_{pizza='mushroom'} Eats)
			))
			\cross
			\rename_{age2} (\project_{age} (
				Person
				\join
				(\select_{pizza='mushroom'} Eats)
			))
		))
	)	
);	

Q8. Find all pizzerias that serve only pizzas eaten by people over 30. 

\project_{*} (
	(\project_{pizzeria} Serves)
	
	\diff
	
	\project_{pizzeria} (
		(\project_{pizzeria, pizza} Serves)
		\diff
		\project_{pizzeria, pizza} (
			\project_{pizza} (
				(\select_{age>30} Person) \join Eats)
			\cross (\project_{pizzeria} Serves)
		)
	)
);


Q9. Find all pizzerias that serve every pizza eaten by people over 30. 

\project_{*} (
	(\project_{pizzeria} Serves)
	
	\diff
	
	\project_{pizzeria} (
		(\project_{pizzeria} Serves)
		\cross
		\project_{pizza} (
			((\select_{age>30} Person) \join Eats)
		)
		\diff
		(\project_{pizzeria, pizza} Serves)
	)
);